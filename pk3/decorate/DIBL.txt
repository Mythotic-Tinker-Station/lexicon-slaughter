ACTOR Lexicon_DIBL_Pistol : DoomWeapon
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Weapon.SlotNumber 2
	Obituary "$OB_MPPISTOL"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	+WEAPON.CHEATNOTWEAPON
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Inventory.PickupSound "DIBL/DSWPNUP"
	Tag "$TAG_PISTOL"
	Radius 20
	Height 16
	States
	{
	Ready:
		PISG A 1 A_WeaponReady
		Loop
	Deselect:
		PISG A 1 A_Lower
		Loop
	Select:
		PISG A 1 A_Raise
		Loop
	Fire:
		PISG A 4
		PISG B 0 A_FireBullets(5.6,0,1,5)
		PISG B 0 A_PlaySound("DIBL/DSPISTOL",CHAN_WEAPON)
		PISG B 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		Goto Ready
	Flash:
		PISF A 7 Bright A_Light1
		Goto LightDone
		PISF A 7 Bright A_Light1
		Goto LightDone
 	Spawn:
		PIST A -1
		Stop
	}
}

ACTOR Lexicon_DIBL_Shotgun : DoomWeapon
{
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$DIBL_GOTSHOTGUN"
	Inventory.PickupSound "DIBL/DSWPNUP"
	Obituary "$OB_MPSHOTGUN"
	Tag "$TAG_SHOTGUN"
	States
	{
	Ready:
		SHTG A 1 A_WeaponReady
		Loop
	Deselect:
		SHTG A 1 A_Lower
		Loop
	Select:
		SHTG A 1 A_Raise
		Loop
	Fire:
		SHTG A 3
		SHTG A 0 A_FireBullets(5.6,0,7,5)
		SHTG A 0 A_PlaySound("DIBL/DSSHOTGN",CHAN_WEAPON)
		SHTG A 7 A_GunFlash
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	Flash:
		SHTF A 4 Bright A_Light1
		SHTF B 3 Bright A_Light2
		Goto LightDone
	Spawn:
		SHOT A -1
		Stop
	}
}

ACTOR Lexicon_DIBL_Chaingun : DoomWeapon
{
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Weapon.SlotNumber 4
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTCHAINGUN"
	Inventory.PickupSound "DIBL/DSWPNUP"
	Obituary "$OB_MPCHAINGUN"
	Tag "$TAG_CHAINGUN"
	States
	{
	Ready:
		CHGG A 1 A_WeaponReady
		Loop
	Deselect:
		CHGG A 1 A_Lower
		Loop
	Select:
		CHGG A 1 A_Raise
		Loop
	Fire:
		CHGG A 0 A_FireBullets(5.6,0,1,5)
		CHGG A 0 A_PlaySound("DIBL/DSPISTOL",CHAN_WEAPON)
		CHGG A 4 A_GunFlash
		CHGG B 0 A_FireBullets(5.6,0,1,5)
		CHGG B 0 A_PlaySound("DIBL/DSPISTOL",CHAN_WEAPON)
		CHGG B 4 A_GunFlash("Flash2")
		CHGG B 0 A_ReFire
		Goto Ready
	Flash:
		CHGF A 5 Bright A_Light1
		Goto LightDone
	Flash2:
		CHGF B 5 Bright A_Light2
		Goto LightDone
	Spawn:
		MGUN A -1
		Stop
	}
}

ACTOR Lexicon_DIBL_RocketLauncher : DoomWeapon
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Weapon.SlotNumber 5
	+WEAPON.NOAUTOFIRE
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$DIBL_GOTLAUNCHER"
	Inventory.PickupSound "DIBL/DSWPNUP"
	Tag "$TAG_ROCKETLAUNCHER"
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		Loop
	Deselect:
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		MISG B 8 A_GunFlash
		MISG B 12 A_FireCustomMissile("Lexicon_DIBL_Rocket")
		MISG B 0 A_ReFire
		Goto Ready
	Flash:
		MISF A 3 Bright A_Light1
		MISF B 4 Bright
		MISF CD 4 Bright A_Light2
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

ACTOR Lexicon_DIBL_Rocket : Rocket
{
	SeeSound "DIBL/DSRLAUNC"
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	}
}

ACTOR Lexicon_DIBL_DoomImp : DoomImp
{
	Speed 20
	States
	{
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_CustomComboAttack("DoomImpBall", 32, 3 * random(1, 8), "imp/melee")
		Goto See
	}
}

ACTOR Lexicon_DIBL_DoomImpBall : DoomImpBall { Speed 20 }

ACTOR Lexicon_DIBL_Cacodemon : Cacodemon
{
	States
	{
	Death:
		HEAD G 8 A_NoBlocking
		HEAD H 8 A_Scream
		HEAD I 8
		HEAD J 8
		HEAD K 8
		LY00 G -1 A_SetFloorClip
		Stop
	Raise:
		LY00 G 8 A_UnSetFloorClip
		HEAD KJIHG 8
		Goto See
	}
}

ACTOR Lexicon_DIBL_Cyberdemon : Cyberdemon
{
	States
	{
	Missile:
		CYBR E 6 A_FaceTarget
		CYBR F 12 A_CustomMissile("Lexicon_DIBL_Rocket")
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CustomMissile("Lexicon_DIBL_Rocket")
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CustomMissile("Lexicon_DIBL_Rocket")
		Goto See
	}
}

ACTOR Lexicon_DIBL_Zombieman : Zombieman  { AttackSound "DIBL/DSPISTOL" }
ACTOR Lexicon_DIBL_ShotgunGuy : ShotgunGuy  { AttackSound "DIBL/DSSHOTGN" }
ACTOR Lexicon_DIBL_ChaingunGuy : ChaingunGuy  { AttackSound "DIBL/DSSHOTGN" }
ACTOR Lexicon_DIBL_Demon : Demon  { Speed 20 }
ACTOR Lexicon_DIBL_SpiderMastermind : SpiderMastermind  { AttackSound "DIBL/DSSHOTGN" }